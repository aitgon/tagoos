CHROM=os.getenv('CHROM').split()
GENOME1K_DIR=os.getenv('GENOME1K_DIR')
GENOME1K_OUT_DIR=os.getenv('GENOME1K_OUT_DIR')
HOME = os.getenv('HOME')
THREADS = int(os.getenv('THREADS'))
GENOMIC_REGION_BED = os.getenv('GENOMIC_REGION_BED')

rule all:
    input:
        expand(os.path.join(GENOME1K_DIR, "vcf_gz/chr{chr}.vcf.gz"), chr=CHROM),
        plink_bed = expand(os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink.bed"), chr=CHROM),
        peak_bed = expand(os.path.join(GENOME1K_DIR, "peak_bed/{chr}/chr{chr}_peak.bed"), chr=CHROM),
        regional_peak_bed = expand(os.path.join(GENOME1K_OUT_DIR, "peak_bed/chrom/{chr}/chr{chr}.bed"), chr=CHROM),
        regional_rsid = expand(os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}.rsid"), chr=CHROM),
        regional_plink_bed = expand(os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_plink.bed"), chr=CHROM),

rule exclude_dupvar:
    input:
        regional_dupvar_plink_bim = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar_plink.bim"),
        regional_dupvar_plink_bed = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar_plink.bed"),
    params:
        regional_dupvar_plink_bed = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar_plink"),
        regional_plink_bed = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_plink"),
    output:
        regional_dupvar_rsid = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar.rsid"),
        regional_plink_bed = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_plink.bed"),
    threads : THREADS
    shell:
        """LC_ALL=C sort -k2,2 {input.regional_dupvar_plink_bim} |cut -f2,2 |uniq -d >{output.regional_dupvar_rsid};
            plink --exclude {output.regional_dupvar_rsid} --bfile {params.regional_dupvar_plink_bed} --make-bed --out {params.regional_plink_bed}"""

rule regional_plink_bed:
    input:
        plink_bed = os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink.bed"),
        regional_rsid = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}.rsid"),
    params:
        plink_bed = os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink"),
        regional_dupvar_plink_bed = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar_plink"),
    output:
        regional_dupvar_plink_bed = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar_plink.bed"),
        regional_dupvar_plink_bim = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}_dupvar_plink.bim"),
    shell:
        """plink --extract {input.regional_rsid} --bfile {params.plink_bed} --make-bed --out {params.regional_dupvar_plink_bed}"""

rule regional_rsid:
    input:
        regional_peak_bed = os.path.join(GENOME1K_OUT_DIR, "peak_bed/chrom/{chr}/chr{chr}.bed"),
    output:
        regional_rsid = os.path.join(GENOME1K_OUT_DIR, "plink_bed/{chr}/chr{chr}.rsid"),
    shell:
        """cut -f4,4 {input.regional_peak_bed} >{output.regional_rsid}"""

rule regional_peak_bed:
    input:
        peak_bed = os.path.join(GENOME1K_DIR, "peak_bed/{chr}/chr{chr}_peak.bed"),
        genomic_region_bed = GENOMIC_REGION_BED,
    output:
        regional_peak_bed = os.path.join(GENOME1K_OUT_DIR, "peak_bed/chrom/{chr}/chr{chr}.bed"),
    shell:
        """bedtools intersect --sorted -a {input.peak_bed} -b {input.genomic_region_bed} | sort -u -k1,1 -k2,2n >{output.regional_peak_bed}"""

rule create_peak_bed:
    input:
        plink_bim = os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink.bim"),
    output:
        peak_bed = os.path.join(GENOME1K_DIR, "peak_bed/{chr}/chr{chr}_peak.bed"),
    threads: THREADS
    shell:
        """awk '{{print "chr"$1"\t"$4-1"\t"$4"\t"$2}}' {input.plink_bim} |sort -k1,1 -k2,2n --parallel {threads} -o {output.peak_bed}"""

rule filter_eur_convert_bed_maf_snps_only:
    input:
        eur_ped_list = os.path.join(GENOME1K_DIR, "eur.ped"),
        vcf_gz = os.path.join(GENOME1K_DIR, "vcf_gz/chr{chr}.vcf.gz"),
    params:
        plink_bed = os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink"),
    output:
        plink_bed = os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink.bed"),
        plink_bim = os.path.join(GENOME1K_DIR, "plink_bed/{chr}/chr{chr}_plink.bim"),
    shell:
        """plink --keep {input.eur_ped_list} --vcf {input.vcf_gz} --maf --snps-only --list-duplicate-vars ids-only suppress-first --make-bed --out {params.plink_bed}"""


rule list_eur_pop:
    output:
        eur_txt = os.path.join(GENOME1K_DIR, "eur.txt"),
        eur_ped_list = os.path.join(GENOME1K_DIR, "eur.ped"),
    shell:
        """"wget ftp://ftp-trace.ncbi.nih.gov/1000genomes/ftp/release/20130502/integrated_call_male_samples_v3.20130502.ALL.panel -q -O - |grep EUR |cut -f2,2 |sort -u >{output.eur_txt}; wget ftp://ftp-trace.ncbi.nih.gov/1000genomes/ftp/release/20130502/integrated_call_samples.20130502.ALL.ped -q -O - |grep -f eur.txt >{output.eur_ped_list}"""

rule download_genome1k:
    params:
        chr = "{chr}",
        url = "ftp://ftp-trace.ncbi.nih.gov/1000genomes/ftp/release/20130502/ALL.chr{chr}.phase3_shapeit2_mvncall_integrated_v5a.20130502.genotypes.vcf.gz",
    output:
        vcf_gz = os.path.join(GENOME1K_DIR, "vcf_gz/chr{chr}.vcf.gz"),
    shell:
        """wget {params.url} -O {output.vcf_gz}"""

