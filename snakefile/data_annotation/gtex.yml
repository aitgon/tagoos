ANNOTATION_DIC=os.getenv('ANNOTATION_DIC')
GTEX_DATA_DIR = os.getenv('GTEX_DATA_DIR')
THREADS = int(os.getenv('THREADS'))

rule proc1:
    input:
        bed=os.path.join(GTEX_DATA_DIR, "proc1.bed"),
    output:
        bed=os.path.join(GTEX_DATA_DIR, "gtex.bed"),
    shell:
        """cat {input.bed} |tr -d '\r' |awk 'NF==4{{print}}{{}}' >{output.bed};"""

rule join_bed_annotation:
    input:
        id_bed=os.path.join(GTEX_DATA_DIR, "gtex_id_sortedk4.bed"),
        annotation_dic=ANNOTATION_DIC,
    output:
        bed=os.path.join(GTEX_DATA_DIR, "proc1.bed"),
    shell:
        """join -1 5 -2 1 {input.id_bed} {input.annotation_dic} |awk 'BEGIN{{OFS="\t"}}{{print $2,$3,$4,$8"."$7"."$6}}' >{output.bed}"""

rule sort_id_bed:
    input:
        id_bed=os.path.join(GTEX_DATA_DIR, "gtex_id.bed"),
    output:
        id_bed=os.path.join(GTEX_DATA_DIR, "gtex_id_sortedk4.bed"),
    threads: THREADS
    shell:
        """sort --parallel {THREADS} -k5,5 {input.id_bed} -o {output.id_bed}"""

rule gunzip:
    input:
        txt_gz_untared=os.path.join(GTEX_DATA_DIR, "txt_gz.untared"),
        annotation_dic=ANNOTATION_DIC,
    output:
        id_bed=os.path.join(GTEX_DATA_DIR, "gtex_id.bed"),
    params:
        GTEx_Analysis_v6p_eQTL_dir=os.path.join(GTEX_DATA_DIR, "GTEx_Analysis_v6p_eQTL"),
    shell:
        """cat {input.annotation_dic} |cut -f3,3 |while read LABEL; do FN=$LABEL; gunzip -c {params.GTEx_Analysis_v6p_eQTL_dir}/${{LABEL}}_Analysis.v6p.signif_snpgene_pairs.txt.gz |awk -F'[\t_]' -v LABEL="$LABEL" 'BEGIN{{OFS=" ";}}NR>1{{print "chr"$1,$2-1,$2,$15,LABEL"_Analysis.v6p.signif_snpgene_pairs.txt.gz"}}'; done >{output.id_bed}"""

rule untar:
    input:
        tar=os.path.join(GTEX_DATA_DIR, "GTEx_Analysis_v6p_eQTL.tar"),
    params: gtex_dir=os.path.join(GTEX_DATA_DIR, "GTEx_Analysis_v6p_eQTL")
    threads: THREADS
    output:
        txt_gz_untared=os.path.join(GTEX_DATA_DIR, "txt_gz.untared"),
    shell:
        """tar xvf {input.tar}; touch {output.txt_gz_untared}"""

rule download_tar:
    params: gtex_data_dir=GTEX_DATA_DIR
    output:
        tar=os.path.join(GTEX_DATA_DIR, "GTEx_Analysis_v6p_eQTL.tar"),
    shell:
        """wget http://www.gtexportal.org/static/datasets/gtex_analysis_v6p/single_tissue_eqtl_data/GTEx_Analysis_v6p_eQTL.tar --directory-prefix={params.gtex_data_dir};"""

